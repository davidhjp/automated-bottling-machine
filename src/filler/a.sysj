filler(
		input signal bottleAtPos2,dosUnitEvac,dosUnitFilled;
		output signal valveInjectorOnOff, valveInletOnOff, dosUnitValveRetract, dosUnitValveExtend ;
		output boolean channel fillerReady,requestCap,filler_detect;
		input boolean channel trig_filler;
		)
->
{

	{
		loop{
			receive trig_filler;

			abort(dosUnitFilled){
				{
					sustain valveInjectorOnOff;
				}
				||
				{
					sustain dosUnitValveRetract;
				}
			}

			abort(dosUnitEvac){
				{
					sustain valveInletOnOff;
				}
				||
				{
					sustain dosUnitValveExtend;
				}
			}

			// Let the rotary know that a cap neeeds to be placed in the next turn
			send requestCap(true);
			pause;

			trap(T){{send fillerReady(true);} || { waitl(3s); exit(T);}}
		}
	}
	||
	{
		loop {
			boolean b = false;
			present(bottleAtPos2){
				b = true;
			}
			send filler_detect(b);
			waitl(50 ms);
			pause;
		}
	}
}
